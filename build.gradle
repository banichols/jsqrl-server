plugins {
    id 'nebula.provided-base' version '3.0.3'
}

group 'org.jsqrl'
version '0.1.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'nebula.provided-base'
apply plugin: 'maven'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile('net.i2p.crypto:eddsa:0.1.0')
    compile('org.slf4j:slf4j-api:1.7.21')
    provided('org.projectlombok:lombok:1.16.8')
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.3.0'
    testCompile('ch.qos.logback:logback-classic:1.1.7')
    testCompile('org.mockito:mockito-all:1.10.19')
}

task createPom  {
    pom {
        project {
            name 'JSQRL Server'
            description 'Java web server implementation of the SQRL authentication protocol'
            artifactId rootProject.name
            url 'https://github.com/banichols/jsqrl-server'
            inceptionYear '2016'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/banichols/jsqrl-server'
            }
            developers {
                developer {
                    id 'banichols'
                    name 'Brent Nichols'
                }
            }
        }
    }.writeTo("pom.xml")
}